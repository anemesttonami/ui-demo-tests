plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
}

allure {
    report {
        version.set('2.34.1')
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set('2.29.1')
                enabled.set(true)
            }
        }
    }
}

repositories {
    mavenCentral()
}

def webdrivermanagerVersion = "5.6.3"

dependencies {
    testImplementation("io.github.bonigarcia:webdrivermanager:$webdrivermanagerVersion")
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.3')
    testImplementation('com.codeborne:selenide:6.19.1')
    testImplementation('io.qameta.allure:allure-selenide:2.19.0')
    testImplementation('org.slf4j:slf4j-log4j12:2.0.14')
    testCompileOnly('org.projectlombok:lombok:1.18.38')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.38')
    testImplementation('org.aeonbits.owner:owner:1.0.12')
    testImplementation('com.codeborne:pdf-test:1.5.0')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('smoke', Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
}

tasks.register('regress', Test) {
    useJUnitPlatform {
        includeTags("regress")
    }
}