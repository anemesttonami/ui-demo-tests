plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
}

allure {
    report {
        version.set('2.34.1')
    }
    adapter {
        aspectjWeaver.set(true) // это чтобы работало @Step
        frameworks {
            junit5 {
                adapterVersion.set('2.29.1')
                enabled.set(true) // явно включает адаптер , рекомендуется указывать
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.3')
    testImplementation('com.codeborne:selenide:6.19.1')
    testImplementation('io.qameta.allure:allure-selenide:2.19.0')
    testImplementation('org.slf4j:slf4j-log4j12:2.0.14')
    testCompileOnly('org.projectlombok:lombok:1.18.38')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.38')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8' // без этой настройки могут возникать проблемы с кодировкой, если в коде есть кириллица
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    systemProperty "remoteLaunch", System.getProperty("remoteLaunch", "false")
}

tasks.register('smoke', Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
}

tasks.register('regress', Test) {
    useJUnitPlatform {
        includeTags("regress")
    }
}